#GAG {似乎有些疲惫，但是仍然十分有活力。 )}
#GAG {动作似乎开始有点不太灵光，但是仍然有条不紊。 )}
#GAG {气喘嘘嘘，看起来状况并不太好。 )}
#GAG {看起来可能有些累了。 )}
#GAG {看起来充满活力，一点也不累。 )}

#TICKER {ticker_feedback_loop} {hp} {15};

#ALIAS {feedback_loop} {
    say $npc_list;
    #IF {$vitals[饮水] < $vitals[饮水_max]} {#ECHO {drink tong};drink tong};
    #LOCAL {hp_percentage} {0};
    #MATH {hp_percentage} {$vitals[气血] * 100 / $vitals[气血_max]};

    #IF {$hp_percentage >= 50} {
        #FOREACH {$npc_list[%*]} {npc} {
            #ECHO {fight $npc};
            fight $npc
        };
    } {
        #IF {$hp_percentage < 20} {
            #ECHO {surrender};
            surrender;
        };
    };

    #IF {$fight_stats[start_epoch]} {
        #FORMAT {epoch} {%T};
        #MATH {fight_stats[duration]} {$epoch - $fight_stats[start_epoch]};
        #MATH {fight_stats[经验_delta]} {$vitals[经验] - $fight_stats[经验0]};
        #MATH {fight_stats[潜能_delta]} {$vitals[潜能] - $fight_stats[潜能0]};
        #MATH {fight_stats[经验/min]} {$fight_stats[经验_delta] * 60 / $fight_stats[duration]};
        #MATH {fight_stats[潜能/min]} {$fight_stats[潜能_delta] * 60 / $fight_stats[duration]};
    } {
        init_fight;
        #FORMAT {fight_stats[start_epoch]} {%T};
        #VARIABLE {fight_stats[character]} {$username};
        #VARIABLE {fight_stats[经验0]} {$vitals[经验]};
        #VARIABLE {fight_stats[潜能0]} {$vitals[潜能]};
    };
    #LOCAL {duration_in_min} {0};
    #MATH {duration_in_min} {$fight_stats[duration] / 60};
    #FORMAT {msg} {经验: %d (+%d), 潜能: %d (+%d), 时长: %d分钟} {$vitals[经验]} {$fight_stats[经验/min]} {$vitals[潜能]} {$fight_stats[潜能/min]} {$duration_in_min};
    #ECHO {$msg};
}

#ALIAS {init_fight} {
    #ECHO {initialize fight};
    remove all;
    drop all;
    wear cloth;
    set no_give 1;
    set no_msg 1;
    set mute;
    enable dodge none;
}

#ACTION {您要将另一个连线中的相同人物赶出去，取而代之吗？} {
    #DELAY {5} {
        #IF {"$control[disable_takeover]" == "y"} {
            n;
        } {
            y;
        };
    }
}

#ACTION {重新连线完毕。} {
    hp;
    l;
}

#ACTION {^( 你摇头晃脑、歪歪斜斜地站都站不稳，眼看就要倒在地上。 )} {
    surrender;
    #NOP #DELAY {3} { #echo shuicmd;shuicmd; }
}

#ACTION {慢慢睁开眼睛，清醒了过来。} {
    surrender;
}

#ACTION {^%s饮水：%s%d/%s%d%s经验：%.%d} {
    #VAR {vitals[饮水]}{%3};
    #VAR {vitals[饮水_max]}{%5};
    #VAR {vitals[经验]}{%8};

    #DELAY {1} {
        #ECHO feedback_loop;
        feedback_loop;
    }
}