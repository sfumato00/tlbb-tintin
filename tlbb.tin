#CONFIG charset GBK1TOUTF8;
#CONFIG {REPEAT ENTER} {ON};
#CONFIG history 2000;

#CLASS paths read {./paths.tin};
#CLASS functions read {./pkg_functions.tin};
#CLASS room read {./pkg_room.tin};

#EVENT {VARIABLE UPDATED vitals[精神_max]} {
    #VARIABLE {xunhuan[max_xiqi_full]} {@RestoreFull{0;$vitals[精神_max];$xunhuan[min_restore]}};
}

#EVENT {VARIABLE UPDATED vitals[气血_max]} {
    #VARIABLE {xunhuan[max_yunqi_full]} {@RestoreFull{0;$vitals[气血_max];$xunhuan[min_restore]}};
}

#nop {login}

#ACTION {^Please input gb or big5:} {gb} {1};

#ACTION {^您的英文名字(新人登录请输入new)：} {
    $username;$password;
} {1};

#ACTION {^您要将另一个连线中的相同人物赶出去，取而代之吗？(y/n)} {#DELAY 30 {n;}};

#ACTION {重新连线完毕。} {
    cmd {look};
    cmd {score};
    cmd {hp};
    init_task;
} {5};


#NOP {common}
#ALIAS {log} {
    #LOG APPEND %1;
    #DELAY {delay_log} {
        #LOG OFF;
    } {%2};
};


#ACTION {^%s气血：%s%d/%s%d%s(%s%d%)%s内力：%s%d%s/%s%d%s(%*)} {
    #VARIABLE {vitals[气血]}{%3};
    #VARIABLE {vitals[气血_max]}{%5};
    #VARIABLE {vitals[气血%]}{%8};
    #VARIABLE {vitals[内力]}{%11};
    #VARIABLE {vitals[内力_max]}{%14};
    #VARIABLE {vitals[内力%]}{%16};
} {5};

#ACTION {^%s精神：%s%d/%s%d%s(%s%d%)%s潜力：%s%d%s/%s%d%s(%*)} {
    #VARIABLE {vitals[精神]}{%3};
    #VARIABLE {vitals[精神_max]}{%5};
    #VARIABLE {vitals[精神%]}{%8};
    #VARIABLE {vitals[潜力]}{%11};
    #VARIABLE {vitals[潜力_max]}{%14};
    #VARIABLE {vitals[潜力%]}{%16};
} {5};

#ACTION {^%s食物：%s%d/%s%d%s潜能：%.%d} {
    #VARIABLE {vitals[食物]}{%3};
    #VARIABLE {vitals[食物_max]}{%5};
    #VARIABLE {vitals[潜能]}{%8};
} {5};

#ACTION {^%s饮水：%s%d/%s%d%s经验：%.%d} {
    #VARIABLE {vitals[饮水]}{%3};
    #VARIABLE {vitals[饮水_max]}{%5};
    #VARIABLE {vitals[经验]}{%8};
} {5};

#ACTION {^%s%S%s(%S)%s-%s%S%s%d/%.%d} {
    #VARIABLE {skills[%4]} {%9};
} {5};

#ACTION {^ 根骨%s%d%s胆识%s%d%s悟性%s%d%s膂力%s%d%s容貌%s%d} {
    #VARIABLE {score[根骨]} {%2};
    #VARIABLE {score[胆识]} {%5};
    #VARIABLE {score[悟性]} {%8};
    #VARIABLE {score[膂力]} {%11};
    #VARIABLE {score[容貌]} {%14};
} {5};

#ACTION {^你的「%u」进步了！$} {cmd {skills};}


#nop 练功模块

#ALIAS {练功} {

    #SWITCH {"%1"} {

        #CASE {"on"} {
            #ECHO {开启 fight 模块};
            #CLASS fight kill;
            #CLASS fight read {./pkg_fight.tin};
            #TICKER {ticker_feedback_loop} {cmd {hp};} {15};
            #DELAY {5} {
                cmd {hp};
            };
        };

        #CASE {"off"} {
            #ECHO {关闭 fight 模块};
            #CLASS fight kill;
            #TICKER {ticker_feedback_loop} {cmd {hp}} {15};
            cmd {surrender};
        };

        #DEFAULT {
            #ECHO {supported arguments are: on, off};
        };
    };
} {5};


#NOP 降经验
#ALIAS {降经验} {

    #SWITCH {"%1"} {

        #CASE {"on"} {
            #ECHO {开启降经验模块};
            #CLASS downexp kill;
            #CLASS downexp read {./pkg_downexp.tin};
        };

        #CASE {"off"} {
            #ECHO {关闭降经验模块};
            #CLASS downexp kill;
        };

        #DEFAULT {
            #ECHO {supported arguments are: on, off};
        };
    };
} {5};


#NOP 九阳
#ALIAS {九阳} {

    #SWITCH {"%1"} {

        #CASE {"on"} {
            #ECHO {开启九阳模块};
            #CLASS jiuyang kill;
            #CLASS jiuyang read {./pkg_jiuyang.tin};
            cmd {exert powerup};
        };

        #CASE {"off"} {
            #ECHO {关闭九阳模块};
            #CLASS jiuyang kill;
        };

        #DEFAULT {
            #ECHO {supported arguments are: on, off};
        };
    };
} {5};



#NOP 循环

#ALIAS {循环} {
    #SWITCH {"%1"} {
        #CASE {"off"} {
            #ECHO {关闭 循环 模块};
            #CLASS xunhuan kill;
            #UNALIAS {feedback_loop};
            #TICKER {ticker_feedback_loop} {cmd {hp}} {15};
        };

        #CASE {"师父"} {
            #ECHO {开启 循环 - 师父 模块};
            #CLASS xunhuan kill;
            #CLASS xunhuan read {./pkg_xunhuan.tin};

            #ALIAS {feedback_loop} {
                feedback_loop_master;
            } {5};

            #DELAY {init} {
                #TICKER {ticker_feedback_loop} {hp} {1.5};
                cmd {hp};
            } {3};
        };

        #CASE {"学习"} {
            #IF {"%2" == ""} {
                #ECHO {必须指定求教对象};
                #RETURN {必须指定求教对象};
            };

            #IF {"%3" == ""} {
                #VARIABLE {xunhuan[js_per_learn]} {350};
            } {
                #VARIABLE {xunhuan[js_per_learn]} {%3};
            };

            #ECHO {开启 循环 - 学习 模块};
            #VARIABLE {learn[master]} {%2};

            #CLASS xunhuan kill;
            #CLASS xunhuan read {./pkg_xunhuan.tin};

            #ALIAS {feedback_loop} {
                feedback_loop_learn;
            } {5};

            #DELAY {init} {
                #TICKER {ticker_feedback_loop} {hp} {1.5};
                update_learn_skill;
                cmd {hp};
            } {3};
            cmd {skills};
        };

        #CASE {"吐纳"} {
            #ECHO {开启 循环 - 吐纳 模块};

            #CLASS xunhuan kill;
            #CLASS xunhuan read {./pkg_xunhuan.tin};

            #ALIAS {feedback_loop} {
                feedback_loop_tuna;
            } {5};

            #DELAY {init} {
                #TICKER {ticker_feedback_loop} {hp} {1.5};
                cmd {hp};
            } {3};
        };

        #CASE {"读书"} {
            #ECHO {开启 循环 - 读书 模块};

            #CLASS xunhuan kill;
            #CLASS xunhuan read {./pkg_xunhuan.tin};
            #VARIABLE {dushu[book]} {%2};
            #ALIAS {feedback_loop} {
                feedback_loop_dushu;
            } {5};

            #DELAY {init} {
                #TICKER {ticker_feedback_loop} {hp} {1.5};
                cmd {hp};
            } {3};
        };

        #CASE {"传功"} {
            #IF {"%2" == ""} {
                #ECHO {必须指定传功对象};
                #RETURN {必须指定传功对象};
            };

            #ECHO {开启 循环 - 传功 模块};

            #VARIABLE {chuangong[target]} {%2};
            #CLASS xunhuan kill;
            #CLASS xunhuan read {./pkg_xunhuan.tin};

            #ALIAS {feedback_loop} {
                feedback_loop_xfer;
            } {5};

            #DELAY {init} {
                #TICKER {ticker_feedback_loop} {hp} {1.5};
                cmd {hp};
            } {3};
        };

        #DEFAULT {
            #ECHO {unsupported argument.};
        };
    };
} {5};


#ALIAS {北冥} {

    #SWITCH {"%1"} {

        #CASE {"on"} {
            #ECHO {开启北冥模块};
            #CLASS beiming kill;
            #CLASS beiming read {./pkg_beiming.tin};
        };

        #CASE {"off"} {
            #ECHO {关闭北冥模块};
            #CLASS beiming kill;
        };

        #DEFAULT {
            #ECHO {supported arguments are: on, off};
        };
    };
} {5};


#ALIAS {cmd} {
    #ECHO {%1};
    #SEND {%1};
} {5};


#ALIAS {dazuo_max} {
    #IF {$vitals[内力] == $vitals[内力_max] * 2} {#RETURN {};};
    #LOCAL yunqi {@RestoreFull{$vitals[气血];$vitals[气血_max];$xunhuan[min_restore]}};

    #IF {$yunqi > 0} {cmd {yunqi $yunqi};};
    cmd {dazuo $vitals[气血_max]};
    cmd {hp};
} {5};



#NOP 核心触发

#ACTION {^目前权限：} {
    
    #FORMAT {env[init_login]} {%T};
} {1};

