#NOP TLBB main module

#CLASS config read {./config.tin};
#CLASS login read {./pkg/pkg_login.tin};
#CLASS graph READ {./pkg/pkg_map_graph.tin};

#CLASS room read {./pkg/pkg_room.tin};
#CLASS command READ {./pkg/pkg_command.tin};

#CLASS utils read {./pkg/pkg_utils.tin};
#CLASS vitals read {./pkg/pkg_vitals.tin};
#CLASS display read {./pkg/pkg_display.tin};
#CLASS stats read {./pkg/pkg_stats.tin};


#EVENT {VARIABLE UPDATED vitals[精神_max]} {
    #VARIABLE {xunhuan[max_xiqi_full]} {@RestoreFull{0;$vitals[精神_max];$xunhuan[min_restore]}};
}

#EVENT {VARIABLE UPDATED vitals[气血_max]} {
    #VARIABLE {xunhuan[max_yunqi_full]} {@RestoreFull{0;$vitals[气血_max];$xunhuan[min_restore]}};
}

#EVENT {VARIABLE UPDATED env[event]} {
    #VARIABLE {xunhuan[max_yunqi_full]} {@RestoreFull{0;$vitals[气血_max];$xunhuan[min_restore]}};
}

#ACTION {^ 姓名：%s%u ($username)。%s别号：%*。$} {
    #VARIABLE {env[name]} {%2};
};

#ACTION {^ %u性，二千零%u时生人，现年%u$} {
    #VARIABLE {env[sex]} {%1};
    #VARIABLE {env[age]} {%3};
};

#NOP 循环

#ALIAS {循环} {
    #SWITCH {"%1"} {
        #CASE {"off"} {
            #ECHO {关闭 循环 模块};
            #CLASS xunhuan kill;
            #UNALIAS {feedback_loop};
            #TICKER {ticker_feedback_loop} {Cmd {hp}} {15};
        };

        #CASE {"师父"} {
            #ECHO {开启 循环 - 师父 模块};

            #VARIABLE {xunhuan[xfer_target]} {%2};
            #CLASS xunhuan kill;
            #CLASS xunhuan read {./pkg/pkg_xunhuan.tin};

            #ALIAS {feedback_loop} {
                feedback_loop_master;
            } {5};

            #DELAY {init} {
                #TICKER {ticker_feedback_loop} {hp} {1.5};
                Cmd {hp};
            } {3};
        };

        #CASE {"传功"} {
            #IF {"%2" == ""} {
                #ECHO {必须指定传功对象};
                #RETURN {必须指定传功对象};
            };

            #ECHO {开启 循环 - 传功 模块};

            #VARIABLE {xunhuan[xfer_target]} {%2};
            #CLASS xunhuan kill;
            #CLASS xunhuan read {./pkg/pkg_xunhuan.tin};

            #ALIAS {feedback_loop} {
                feedback_loop_xfer;
            } {5};

            #DELAY {init} {
                #TICKER {ticker_feedback_loop} {dazuo 1} {1};
                Cmd {hp};
            } {3};
        };
    };
} {5};


#ALIAS {吐纳} {
    #ECHO {开启 吐纳 模块};
    
    #DELAY {init} {
        #TICKER {ticker_feedback_loop} {check_busy} {1.2};
        #CLASS tuna kill;
        #CLASS tuna read {./pkg/pkg_tuna.tin};
    } {3};
}

#ALIAS {读书} {
    #ECHO {开启 读书 模块};

    #VARIABLE {read[book]} {%1};
    
    #DELAY {init} {
        #TICKER {ticker_feedback_loop} {check_busy} {2};
        #CLASS read kill;
        #CLASS read read {./pkg/pkg_read.tin};
    } {3};
}

#ALIAS {学习} {
    #ECHO {开启 学习 模块};
        #VARIABLE {learn[master]} {%1};
        #IF {"%2" == ""} {
            #VARIABLE {xunhuan[js_per_qn]} {11};
        } {
            #VARIABLE {xunhuan[js_per_qn]} {%2};
        };
        #DELAY {init} {
            #TICKER {ticker_feedback_loop} {check_busy} {2};
            #CLASS learn kill;
            #CLASS learn read {./pkg/pkg_learn.tin};
        } {4};
        score;
        research literate 1;
} {5};

#ALIAS {研究} {
    #ECHO {开启 研究 模块};
        
        #DELAY {init} {
            #TICKER {ticker_feedback_loop} {check_busy} {2};
            #CLASS research kill;
            #CLASS research read {./pkg/pkg_research.tin};
        } {4};
        score;
} {5};

#ALIAS {北冥} {

    #SWITCH {"%1"} {

        #CASE {"on"} {
            #ECHO {开启北冥模块};
            #CLASS beiming kill;
            #CLASS beiming read {./pkg/pkg_beiming.tin};
        };

        #CASE {"off"} {
            #ECHO {关闭北冥模块};
            #CLASS beiming kill;
        };

        #DEFAULT {
            #ECHO {supported arguments are: on, off};
        };
    };
} {5};

#ALIAS {kezhan} {
    

    #SWITCH {"%1"} {

        #CASE {"off"} {
            #ECHO {关闭客栈模块};
            #CLASS kezhan kill;
        };

        #DEFAULT {
            #ECHO {开启客栈模块};
            Cmd {hp;skills;score;enable} {4};
            #CLASS kezhan READ {./pkg/pkg_kezhan.tin};
        };
    };
}

#ALIAS {gun} {
    

    #SWITCH {"%1"} {

        #CASE {"off"} {
            #ECHO {关闭打手枪模块};
            #CLASS gun kill;
        };

        #DEFAULT {
            #ECHO {开启打手枪模块};
            Cmd {hp;skills;score;enable} {4};
            #CLASS gun READ {./pkg/pkg_gun.tin};
        };
    };
}

#ALIAS {fight} {
    

    #SWITCH {"%1"} {

        #CASE {"off"} {
            #ECHO {关闭练功模块};
            #CLASS fight kill;
        };

        #DEFAULT {
            #ECHO {开启练功模块};
            Cmd {hp;skills;score;enable} {4};
            #CLASS fight READ {./pkg/pkg_fight.tin};
        };
    };
}

#ALIAS {dazuo_max} {
    #IF {$vitals[内力] == $vitals[内力_max] * 2} {#RETURN {};};
    #LOCAL yunqi {@RestoreFull{$vitals[气血];$vitals[气血_max];$xunhuan[min_restore]}};

    #IF {$yunqi > 0} {Cmd {yunqi $yunqi};};
    Cmd {dazuo $vitals[气血_max]};
    Cmd {hp};
} {5};

#ALIAS {feedback_loop_idle} {
    #TICKER {ticker_feedback_loop} {Cmd {hp}} {15};
};

#NOP 核心触发

#ACTION {^你目前至少要用 %1 点潜能。$} {
    #VARIABLE {xunhuan[qn_per_res]} {@ParseFlakyNumber{%1}};
} {5};