#CONFIG charset GBK1TOUTF8;
#CONFIG history 200;

#ALIAS {connect} {#ses $username 103.10.85.133 8888 };

#EVENT {SESSION DISCONNECTED} {
    #IF {"$control[disable_reconnect]" == "y"} {
        #ECHO {'reconnect' is disabled.;};
        #DELAY {60};
    } {
        #gts connect;
    }
}

#EVENT {SESSION CONNECTED} {
    #split
};

#nop {login}

#ACTION {^Please input gb or big5:} {gb}

#ACTION {^您的英文名字(新人登录请输入new)：} {
    $username;$password;
}

#ACTION {^您要将另一个连线中的相同人物赶出去，取而代之吗？(y/n)} {#DELAY 30 {n;}};

#nop {common}
#ALIAS {log} {
    #LOG APPEND %1;
    #DELAY {%2} {
        #LOG OFF
    } {5};
}


#ACTION {^%s气血：%s%d/%s%d%s(%s%d%)%s内力：%s%d%s/%s%d%s(%*)} {
    #VAR {vitals[气血]}{%3};
    #VAR {vitals[气血_max]}{%5};
    #VAR {vitals[气血%]}{%8};
    #VAR {vitals[内力]}{%11};
    #VAR {vitals[内力_max]}{%14};
    #VAR {vitals[内力%]}{%16};
};

#ACTION {^%s精神：%s%d/%s%d%s(%s%d%)%s潜力：%s%d%s/%s%d%s(%*)} {
    #VAR {vitals[精神]}{%3};
    #VAR {vitals[精神_max]}{%5};
    #VAR {vitals[精神%]}{%8};
    #VAR {vitals[潜力]}{%11};
    #VAR {vitals[潜力_max]}{%14};
    #VAR {vitals[潜力%]}{%16};
}

#ACTION {^%s食物：%s%d/%s%d%s潜能：%.%d} {
    #VAR {vitals[食物]}{%3};
    #VAR {vitals[食物_max]}{%5};
    #VAR {vitals[潜能]}{%8};
}

#ACTION {^%s饮水：%s%d/%s%d%s经验：%.%d} {
    #VAR {vitals[饮水]}{%3};
    #VAR {vitals[饮水_max]}{%5};
    #VAR {vitals[经验]}{%8};
}

#ACTION {^%s%S%s(%S)%s-%s%S%s%d/%.%d} {
    #VAR {skills[%4]} {%9};
}

#ACTION {^你的「%u」进步了！$} {skills;}


#ALIAS {cal_xh_params} {
    #VARIABLE {xunhuan[xiqi]} {@RestoreFull{0;$vitals[精神_max];$xunhuan[min_restore]}};
    #VARIABLE {xunhuan[yunqi]} {@RestoreFull{0;$vitals[气血_max];$xunhuan[min_restore]}};
};


#FUNCTION Restore {
    #LOCAL curr {%1};
    #LOCAL max {%2};
    #LOCAL min_restore {%3};
    #MATH ret {($max - $curr) / $min_restore * 20};
    #RETURN $ret;
};


#FUNCTION RestoreFull {
    #LOCAL curr {%1};
    #LOCAL max {%2};
    #LOCAL min_restore {%3};

    #LOCAL diff {0};
    #LOCAL multiple {0};

    #MATH {diff} {$max - $curr};
    #MATH multiple {$diff / $min_restore};
    #IF {$multiple * $min_restore < $diff} {
        #MATH multiple {$multiple + 1};
    };

    #MATH ret {$multiple * 20};
    #RETURN $ret;
};


#nop fight
#ALIAS {start_fight} {
    #CLASS fight kill;
    #CLASS fight read {./fight.tin};
    #TICKER {ticker_feedback_loop} {hp} {15};
    hp;
    l;
} {5};


#ALIAS {stop_fight} {
    #CLASS fight kill;
    #TICKER {ticker_feedback_loop} {hp} {15};
    surrender;
} {5};


#NOP 传功
#ALIAS {start_chuangong_learn} {
    #CLASS chuangong_learn kill;
    #CLASS chuangong_learn read {./chuangong_learn.tin};
    score;
    skills;
    hp;
    #DELAY {3} {

        #TICKER {ticker_feedback_loop} {
            feedback_loop;
        } {1};
    };
} {5};


#ALIAS {stop_chuangong_learn} {
    #CLASS chuangong_learn kill;
    #TICKER {ticker_feedback_loop} {hp} {15};
} {5};


#ALIAS {start_xunhuan_master} {
    #CLASS xunhuan kill;
    #CLASS xunhuan read {./pkg_xunhuan.tin};
    hp;
    #DELAY {2} {
        cal_xh_params;
        #EVENT {VARIABLE UPDATED vitals[气血]} {feedback_loop_master};
        #TICKER {ticker_feedback_loop} {hp} {1.6};
    };
} {5};

#ALIAS {start_xunhuan_learn} {
    #CLASS xunhuan kill;
    #CLASS xunhuan read {./pkg_xunhuan.tin};
    #EVENT {VARIABLE UPDATED vitals[内力]} {feedback_loop_learn;};
    #TICKER {ticker_feedback_loop} {hp} {1.6};
    dazuo $vitals[内力];
} {5};

#ALIAS {start_xunhuan_tuna} {
    #CLASS xunhuan kill;
    #CLASS xunhuan read {./pkg_xunhuan.tin};
    #EVENT {VARIABLE UPDATED vitals[内力]} {feedback_loop_tuna;};
    #TICKER {ticker_feedback_loop} {hp} {1.6};
    hp;
} {5};


#ALIAS {stop_xunhuan} {
    #CLASS xunhuan kill;
    #UNEVENT {VARIABLE UPDATED vitals[内力]};
    #TICKER {ticker_feedback_loop} {hp} {15};
} {5};


#ALIAS {start_dong_tuna} {
    #CLASS dong kill;
    #CLASS dong read {./pkg_dong.tin};
    #TICKER {ticker_feedback_loop} {hp} {2};
} {5};


#ACTION {stop_dong} {
    #CLASS dong kill;
    #TICKER {ticker_feedback_loop} {hp} {15};
} {5};


#ALIAS {start_chuangong} {
    #IF {$xunhuan[min_restore] && $xunhuan[max_neili_gain]} {
        #VARIABLE {chuangong[target]} {%1};
        #CLASS chuangong kill;
        #CLASS chuangong read {./chuangong.tin};

        #TICKER {ticker_feedback_loop} {hp;} {1.6};
    } {
        #ECHO {Must set xunhuan[min_restore] and xunhuan[max_neili_gain] to use 传功};
    };
}


#ALIAS {stop_chuangong} {
    #CLASS chuangong kill;
    #TICKER {ticker_feedback_loop} {hp;} {15};
}


