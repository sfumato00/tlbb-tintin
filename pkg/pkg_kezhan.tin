#NOP kezhan module

#CLASS commands READ {./pkg/pkg_command.tin};
#CLASS stats READ {./pkg/pkg_stats.tin};
#CLASS map_graph READ {./pkg/pkg_map_graph.tin};
#CLASS checkbusy READ {./pkg/pkg_checkbusy.tin};
#CLASS learn_function READ {./pkg/pkg_learn_function.tin};


#VARIABLE {KEZHAN} {
    {小客栈 east 和 up} {
        {waiter_id} {waiter}
        {waiter_cn} {店小二}
        {rooms} {
            {1} {s}
            {2} {n}
            {3} {e}
            {4} {w}
        }
    }
    {东客房 south、north、eastup、east、westup、southeast、west、southwest 和 southup} {
        {waiter_id} {xiao cui}
        {waiter_cn} {小翠}
        {rooms} {
            {1} {w}
            {2} {s}
            {3} {e}
            {4} {wu}
            {5} {su}
            {6} {eu}
            {7} {sw}
            {8} {se}
        }
    }
    {登封客栈 west 和 up} {
        {waiter_id} {waiter}
        {waiter_cn} {店小二}
        {rooms} {
            {1} {u;w}
            {2} {u;n}
            {3} {u;e}
        }
    }
    {古城客栈 south 和 up} {
        {waiter_id} {keeper}
        {waiter_cn} {掌柜的}
        {rooms} {
            {1} {u;e}
        }
    }
    {神憩小舍 east、southdown、northwest 和 up} {
        {waiter_id} {fatman}
        {waiter_cn} {董胖子}
        {rooms} {
            {1} {u;w}
            {2} {u;n}
        }
    }
};

#ACTION {笑着对你说道：这位%*，您还没付钱呢！$} {
    #local waiter {$KEZHAN[$Kezhan[hotel_room_key]][waiter_id]};
    Cmd {give 1 gold to $waiter};
    #DELAY 0.5 {
        Cmd {@_get_random_room{}} {2};
    };
} {5};

#ACTION {^掌柜的在你后面咳嗽了一下：“先交一两银子！”。$} {
    #local waiter {$KEZHAN[$Kezhan[hotel_room_key]][waiter_id]};
    Cmd {give 1 gold to $waiter};
    #DELAY 0.2 {go_rest};
}

#ACTION {^请先去交过钱再来住店吧！$} {
    #local waiter {$KEZHAN[$Kezhan[hotel_room_key]][waiter_id]};
    Cmd {give 1 gold to $waiter};
    #DELAY 0.2 {go_rest};
}

#ACTION {^你躺在檀香木小床上，眼望着天花板，发了半天呆，就是睡不着，大概是睡得太多了！$} {
    Cmd {shui} {1};
};

#ACTION {^你一觉醒来，觉得身体完全恢复了} {
    du duan-steps $vitals[精神_max];
    CheckBusy {@_get_random_room{};hp};
};

#ACTION {^你听了%*的指导，似乎有些心得。$} {
    go_rest;
}

#ACTION {^这里已经住满了，请到别间去住吧！} {
    #DELAY {0.5} {
        Cmd {@_get_random_room{}} {2};
    };
}

#ALIAS {go_learn} {
    #EVENT {VARIABLE UPDATED Travel[trip_complete]} {
        #UNEVENT {VARIABLE UPDATED Travel[trip_complete]};
        learn_from_master;
    };
    await_goto {$Kezhan[learn][master_room_key]};
} {5};

#ALIAS learn_from_master {
    #LOCAL qn {1};
    #LOCAL js_p_qn {$Kezhan[learn][js_p_qn]};
    #LOCAL sk_target_lv @GetSkillToLearn{};
    #LOCAL sk {$sk_target_lv[+1]};
    #LOCAL target_lv {$sk_target_lv[+2]};
    #LOCAL sk_curr $skills[$sk];
    #LOCAL sk_curr_lv $sk_curr[lv];
    #LOCAL sk_curr_prof $sk_curr[prof];

    #LOCAL qn_before_level @GetMinQnBeforeLevel{$sk_curr_lv;$sk_curr_prof;$score[悟性]};

    #LOCAL master_id {$Kezhan[learn][master_id]};
    #MATH qn {($vitals[精神_max] - 1) / $js_p_qn};
    
    #IF {$qn > $qn_before_level && ($sk_curr_lv >= 100 || $sk_curr_lv + 1 == $target_lv)} {
        #LOCAL qn $qn_before_level;
    };
    
    #ECHO {qn to use: $qn};
    Cmd {learn $sk from $master_id with $qn} {1};
    Cmd {skills} {1};
}

#ALIAS {go_rest} {
    
    #EVENT {VARIABLE UPDATED Travel[trip_complete]} {
        #IF {"$Travel[trip_complete]" == "1"} {
            @_get_random_room{};
        };
    };
    goto {$Kezhan[hotel_room_key]};
} {5};

#FUNCTION _get_random_room {
    #LOCAL rooms $KEZHAN[$Kezhan[hotel_room_key]][rooms];
    #LOCAL n {};
    #LOCAL x {};
    #LIST rooms SIZE n;
    #MATH x {1d$n};
    #RETURN $rooms[$x];
};


