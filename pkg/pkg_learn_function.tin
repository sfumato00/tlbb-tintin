#NOP functions used by learn
#CLASS stats read {./pkg/pkg_stats.tin};
#CLASS print READ {./pkg/pkg_print.tin};
#CLASS utils READ {./pkg/pkg_utils.tin};

#ALIAS learn_from_master {
    Warn {debug};
    #LOCAL js {%1};
    #IF {"$js" == ""} {
        #LOCAL js $vitals[精神_max];
    };

    #LOCAL do_res {%2};
    #IF {"%2" == ""} {
        #LOCAL do_res {1};
    };

    #IF {$vitals[饮水] < $vitals[饮水_max]} {
        Cmd {drink tong} {1};
    };

    #LOCAL qn {1};
    #LOCAL js_p_qn {$Learn[js_per_qn]};
    #LOCAL sk_target_lv @GetSkillToLearn{};
    #LOCAL sk {*sk_target_lv[+1]};
    #LOCAL target_lv {$sk_target_lv[+1]};

    Info {sk: $sk, target lv: $target_lv};

    #LOCAL sk_curr $skills[$sk];
    #LOCAL sk_curr_lv $sk_curr[lv];
    #LOCAL sk_curr_prof $sk_curr[prof];

    #LOCAL qn_before_level @GetMinQnBeforeLevel{$sk_curr_lv;$sk_curr_prof;$score[悟性]};
    Info {qn_before_level: $qn_before_level, curr level: $sk_curr_lv, target lv: $target_lv};

    #IF {"$qn_before_level" == "0"} {
        #IF {$sk_curr_lv >= 100 && "$do_res" == "1"} {
            Cmd {research $sk $xunhuan[qn_per_res]} {1};
            #RETURN {};
        };
        #ELSEIF {$sk_curr_lv < 100 || $sk_curr_lv + 1 == $target_lv} {
            Cmd {learn $sk from $Learn[master] with 1} {1};
            #RETURN {};
        };
    };

    #LOCAL multiple_before_level {};
    #MATH multiple_before_level {$qn_before_level / 50};

    #LOCAL multiple_allowed_by_js {};
    #LOCAL js_per_fifty_learn {};
    #MATH js_per_fifty_learn {$js_p_qn * 50};
    #MATH multiple_allowed_by_js {($js - 1) / $js_per_fifty_learn};

    #LOCAL m {@Min{$multiple_allowed_by_js;$multiple_before_level}};
    
    Info {qn_before_level: $qn_before_level, m: $m, curr level: $sk_curr_lv, target lv: $target_lv};
    
    #IF {$m > 0} {
        #$m {
            Cmd {learn $sk from $Learn[master] with 50} {1};
        };
    };
    #ELSE {
        
        #LOCAL qn {1};
        #MATH qn {($js - 1) / $js_p_qn};
        #LOCAL next_level {1};
        #MATH next_level {$qn_before_level + 1};
        Warn {qn_before_level: $qn_before_level, qn: $qn, js: $js, js_p_qn: $js_p_qn};
        #LOCAL qn {@Min{$next_level;$qn}};
        
        Cmd {learn $sk from $Learn[master] with $qn} {1};
        #LOCAL remaining_js {0};
        #MATH remaining_js {$js}
    };
}


#ALIAS {learn_master} {
    #LOCAL qn {1};
    #LOCAL js_p_qn {$Learn[js_per_qn]};
    #LOCAL sk_target_lv @GetSkillToLearn{};
    #LOCAL sk {*sk_target_lv[+1]};
    #LOCAL target_lv {$sk_target_lv[+1]};

    Info {sk: $sk, target lv: $target_lv};

    #LOCAL sk_curr $skills[$sk];
    #LOCAL sk_curr_lv $sk_curr[lv];
    #LOCAL sk_curr_prof $sk_curr[prof];

    #IF {$sk_curr_lv >= $target_lv} {
        Info {达到目标等级。停止学习模块};
        stop;
        #RETURN {};
    };
    #LOCAL neili $vitals[内力];
    #LOCAL full_xiqi {@RestoreFull{$vitals[精神];$vitals[精神_max];$xunhuan[min_restore]}};
    #LOCAL max_full_xiqi {@RestoreFull{0;$vitals[精神_max];$xunhuan[min_restore]}};
    #LOCAL neili_allowed_m {0};

    #IF {$neili < $full_xiqi} {
        #RETURN {};
    };
    
    #IF {$full_xiqi > 0} {
        Cmd {xiqi $full_xiqi} {1};
        #MATH {neili} {$neili -$full_xiqi};
    };

    #LOCAL qn_before_level @GetMinQnBeforeLevel{$sk_curr_lv;$sk_curr_prof;$score[悟性]};
    #MATH m {$qn_before_level / 50};
    #MATH n {$qn_before_level % 50};

    Warn {m: $m, n: $n, neili_allowed_m: $neili_allowed_m};

    #IF {$sk_curr_lv >= 100 && "$Learn[res]" == "1" && "$m" == "0" && "$n" == "0"} {
        Cmd {research $sk $xunhuan[qn_per_res]} {1};
        #RETURN {};
    };
    #ELSEIF {$sk_curr_lv < 100 || "$Learn[res]" == "0"} {
        Cmd {learn $sk from $Learn[master] with 1} {1};
        #RETURN {};
    };

    #LOCAL neili_per_learn {0};
    #MATH neili_per_learn {$Learn[js_per_qn] * 50};

    #LOCAL xq {@Restore{0;$neili_per_learn;$xunhuan[min_restore]}};

    #WHILE {@GetCommandCnt{} < 48 && $m > 0 && $neili - $xq >= $max_full_xiqi} {
        Cmd {
            learn $sk from $Learn[master] with 50;
            xiqi $xq;
        } {2};
        #MATH {neili} {$neili -$xq};
        #MATH m {$m - 1};
    };

    #IF {@GetCommandCnt{} < 48 && "$m" == "0" && $n > 0} {
        Cmd {
            learn $sk from $Learn[master] with $n;
        } {1};
    };

    #VARIABLE {peek} {@Peek{}};
    #MATH Busy[next_check] {$peek[epoch] + 12};
} {5};


#FUNCTION GetMinQnBeforeLevel {
    #LOCAL level %1;
    #LOCAL prof %2;
    #LOCAL wx %3;

    #LOCAL next_level {$level};
    #LOCAL diff {0};
    
    #MATH next_level {$next_level + 1};
    #MATH max_pro {$next_level * $next_level + 1};
    #MATH diff {$max_pro - $prof};

    #MATH max_pro_per_qn {$wx * 3 / 4 + 2};
    #MATH res {$diff / $max_pro_per_qn};

    #RETURN $res;
};


#FUNCTION GetSkillToLearn {
    #LOCAL i {};
    #FOREACH {$Learn[skills][%*]} {_skill} {
        #LOCAL sk {*_skill[+1]};
        #LOCAL target_lv {$_skill[+1]};
        #NOP Info {sk: $sk, target_lv: $target_lv};
        #IF {"$skills[$sk]" == "" || $skills[$sk][lv] < $target_lv} {
            Warn {RETURN: sk: $sk, target_lv: $target_lv};
            #RETURN {$_skill};
        };
    };
    #RETURN {};
};

