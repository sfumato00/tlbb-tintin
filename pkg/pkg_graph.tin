#NOP graph-based paths
#CLASS room READ {./pkg/pkg_room.tin};
#CLASS map READ {./pkg/pkg_map.tin};
#CLASS map_functions READ {./pkg/pkg_map_functions.tin};
#CLASS datetime READ {./pkg/pkg_datetime.tin};
#CLASS commands READ {./pkg/pkg_command.tin};

#VARIABLE {Travel[THROTTLE]} {23};

#ALIAS goto {
    travel {$env[room]$env[exits]} {%1};
}

#ALIAS travel {
    #LOCAL src {%1};
    #LOCAL dest {%2};
    #VARIABLE {Travel[dest]} {%2};
    #LOCAL conn {@_dijkstra{$src;$dest}};

    Info {src: $src, dest: $dest};
    Info {stops: $conn[stops]};
    Info {path: $conn[path]};
    Info {dist: $conn[dist]};
    Info {has function?: $conn[function]};

    #VARIABLE Travel[stops] {$conn[stops]};
    #LIST Travel[stops] explode {;};

    #EVENT {VARIABLE UPDATED env[room_update_hook]} {
        #LOCAL u {$Travel[stops][1]};

        PurgeHistory;

        #IF {@_assertRoomExitsKey{$u}} {
            #VARIABLE {travel[started]} {0};

            #IF {"$u" == "$Travel[dest]"} {
                Info {Arrived Final Destination: $Travel[dest]};
                #UNEVENT {VARIABLE UPDATED env[room_update_hook]};
                #VARIABLE {Travel[trip_complete]} {1};
                #RETURN {};
            };

            #LOCAL v {$Travel[stops][2]};
            #LOCAL count @GetCommandCnt{};
            Warn {$MAP[$u][$v][function], $Command[history][cnt], $count, $MAP[$u][$v][dist]};
            #IF {$count > 0} {
                #IF {"$MAP[$u][$v][function]" == "1" && $Command[history][cnt] > $Travel[THROTTLE]
                    || $Command[history][cnt] + $MAP[$u][$v][dist] > $Travel[THROTTLE]} {
                    #DELAY 2 {
                        #VARIABLE {env[room_update_hook]} {1};
                    };
                    #RETURN {};
                };
            };
            Info {$u ==> $v: $MAP[$u][$v][path]};
            #IF {"$MAP[$u][$v][function]" == ""} {
                Cmd {$MAP[$u][$v][path]} {$MAP[$u][$v][dist]};
            };
            #ELSE {
                $MAP[$u][$v][path];
            };            
            Warn {Commands in rate limiting window: $Command[history][cnt]};
            #LIST Travel[stops] DELETE 1 1;
        };
    };
    #IF {"$env[room]" == ""} {
        Cmd {look} {1};
    };
    #ELSE {#VARIABLE env[room_update_hook] {1}};    
};


#FUNCTION _dijkstra {
    #LOCAL src %1;
    #LOCAL dest %2;

    #IF {"$src" == "$dest"} {
        Info {终点已经抵达。};
        #RETURN {
            {path} {}
            {dist} {0}
            {stops} {$dest}
        };
    };

    #IF {"$MAP[$src]" == ""} {
        Warn {无法识别的房间: $src};
        #RETURN {};
    };

    #IF {"$MAP[$src][$dest]" != ""} {
        #RETURN {
            {path} {$MAP[$src][$dest][path]}
            {dist} {$MAP[$src][$dest][dist]}
            {stops} {$src;$dest}
            {function} {$MAP[$src][$dest][function]}
        };
    };

    #LOCAL {dists} {};
    #LOCAL {visited} {};
    #LOCAL {_paths} {};
    #LOCAL {stops} {};
    #LOCAL {is_function} {};
    #LOCAL {u} {};

    #FOREACH {*MAP[%*]} {u} {
        #LOCAL {dists[$u]} {999};
        #LOCAL {visited[$u]} {0};
    };
    #LOCAL {dists[$src]} {0};
    #LOCAL {stops[$src]} {$src};
    #LOCAL {_paths[$src]} {};
    
    #FUNCTION _getNextNode {
        #LOCAL _nextNode {};
        #LOCAL _min_dist {999};
        #LOCAL node {};
        #FOREACH {*dists[%*]} {node} {
            #NOP #ECHO {[DEBUG] node: $node, dists[node]: $dists[$node], visited: $visited[$node]};
            #IF {!$visited[$node] && $dists[$node] < $_min_dist} {
                
                #LOCAL {_min_dist} {$dists[$node]};
                #LOCAL {_nextNode} {$node};
            };
        };
        #RETURN $_nextNode;
    };

    #LOCAL {u} {@_getNextNode{$dists;$visited}};

    #WHILE {"$u" != ""} {
        #LOCAL {visited[$u]} {1};
        #LOCAL v {};
        #FOREACH {*MAP[$u][%*]} {v} {
            #IF {$visited[$v]} {
                #CONTINUE;
            };
            #LOCAL d {};
            #MATH d {$dists[$u] + $MAP[$u][$v][dist]};
            
            #IF {$d < $dists[$v]} {
                #LOCAL {dists[$v]} {$d};
                #MATH {is_function[$v]} {("$MAP[$u][$v][function]" == "1") || ("$is_function[$v]" == "1")};
                #IF {"$_paths[$u]" == ""} {
                    #LOCAL {_paths[$v]} {$MAP[$u][$v][path]};
                } {
                    #LOCAL {_paths[$v]} {$_paths[$u]};
                    #CAT {_paths[$v]} {;$MAP[$u][$v][path]};
                };
                #LOCAL {stops[$v]} {$stops[$u];$v};
            };
        };
        #LOCAL {u} {@_getNextNode{}};
    };

    #LOCAL x {};
    #NOP #ECHO {\n};
    #NOP #ECHO {paths: \n};
    #FOREACH {*_paths[%*]} {x} {
        #NOP #ECHO {    $x:};
        #NOP #ECHO {        distance: $dists[$x]};
        #NOP #ECHO {        path: $_paths[$x]};
        #NOP #ECHO {        stops: $stops[$x]};
    };

    #IF {!$is_function[$dest]} {
        #VARIABLE {MAP[$src][$dest]} {
            {path} {$_paths[$dest]}
            {dist} {$dists[$dest]}
        };
        #CLASS map WRITE {./pkg/pkg_map_cached.tin};
    };

    #RETURN {
        {path} {$_paths[$dest]}
        {dist} {$dists[$dest]}
        {stops} {$stops[$dest]}
        {function} {$is_function[$dest]}
    };
}

#ALIAS Info {
    #ECHO {@Timestamp{} <cfa>[导航] %1};
}

#ALIAS Warn {
    #ECHO {@Timestamp{} <fca>[导航] %1};
}


