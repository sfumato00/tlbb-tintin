#NOP 北冥模块
#NOP 这个是适合比较高级的账号在中门站撸。

#CLASS command READ {./pkg/pkg_command.tin};
#CLASS print READ {./pkg/pkg_print.tin};
#CLASS vitals READ {./pkg/pkg_vitals.tin};
#CLASS stats READ {./pkg/pkg_stats.tin};

#VARIABLE {xi_count} {0};
#VARIABLE {to_yun} {false};

#VARIABLE {Beiming} {
    {xi_count} {0}
    {to_yun} {0}
};

#ACTION {^门开了，从里面走出来一名金衣剑客。$} {
    #IF {$vitals[气血] > $vitals[气血_max] * 0.9} {
        Cmd {#3 exert xi wu shi} {3};
    };
}

#ACTION {在门上敲了几下。$} {
    #IF {$vitals[气血] > $vitals[气血_max] * 0.9} {
        #TICKER temp_tker_xi {
            Cmd {#3 exert xi wu shi} {3};
        } {0.1};
        #DELAY 0.4 {
            #UNTICKER temp_tker_xi;
        };
    };
} {5};


#ACTION {^你要服用哪种药？$} {
    Warn {药用完了。停手。};
    #class beiming KILL;
} {5};

#ACTION {^金衣剑客转身走了进去。$} {
    hp;
    drink tong;
} {5};


#ACTION {^红衣武士转身走进门去。$} {
    hp;
    drink tong;
} {5};


#ACTION {^门又关上了。$} {
    #IF {$vitals[气血%] < 100} {
        Cmd {
            apply xiao-xingxue;
            apply xiao-xingxue;
        } {1};
    };
} {5};


#ACTION {^你运用北冥神功，将%u的内力吸了少许过来储藏于檀中气穴。$} {
    #MATH {Beiming[xi_count]} {$Beiming[xi_count] + 1};

    #IF {$Beiming[xi_count] >= 50} {
        #VARIABLE {Beiming[to_yun]} {1};
        #VARIABLE {Beiming[xi_count]} {0};
    };
} {5};

#ACTION {^你全身放松，坐下来开始将储存在膻中的真气归入气海。$} {
    #VARIABLE {Beiming[to_yun]} {0};
} {5};


#EVENT {VARIABLE UPDATED vitals[last_updated]} {
    #IF {$vitals[气血%] < 100} {
        Cmd {apply xiao-xingxue} {1};
    };
    #ELSEIF {$Beiming[to_yun] && $vitals[气血] == $vitals[气血_max] && $vitals[内力] >= $vitals[内力_max]} {
        Cmd {exert yun} {1};
    };

    #LOCAL {target} {0};
    #LOCAL {diff} {0};
    #MATH {target} {$score[根骨] * 150};
    #math diff {$target - $vitals[内力_max]};

    #IF {"$diff" == "0"} {
        #CLASS beiming KILL;
        #TICKER {ticker_feedback_loop} {hp;} {15};
        #RETURN {};
    };
    Info {目标最大内力：$target, xi_count: $Beiming[xi_count]};
    Cmd {say 还差$diff} {1};
};


Cmd {score;hp} {2};