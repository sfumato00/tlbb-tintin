#ALIAS {返回战斗地点} {
    #ECHO {返回战斗地点};

    #DELAY {6} {
        #ECHO {距离返回战斗地点还有 18 秒};
        #DELAY {6} {
            #ECHO {距离返回战斗地点还有 12 秒};
            #DELAY {6} {
                #ECHO {距离返回战斗地点还有 6 秒};
                #EVENT {VARIABLE UPDATED env[room_updated]} {
                    #IF {"$env[room]" == "$downexp[room]"} {
                        #UNEVENT {VARIABLE UPDATED env[room_updated]};
                        练功;
                    };
                };
                城隍庙-练功房间;
            };
        };
    };
} {5};

#ALIAS {你死了} {
    #ECHO {debug: 鬼门关触发};
    #LOCAL {exp_to_erase} {0};
    #IF {$downexp[target_exp]} {
        #MATH {exp_to_erase} {$vitals[经验] - $downexp[target_exp]};
    };

    #ECHO {降 $exp_to_erase 经验};
    鬼门关-黑店;
    #ECHO {tell qian $exp_to_erase};
    tell qian $exp_to_erase;
    y;
    #2 w;
    ask $username about 回家;
} {5};


#ALIAS {去降经验} {
    #UNEVENT {VARIABLE UPDATED fight_stats[epoch][last]};
    stop;
    quit;
}

#EVENT {VARIABLE UPDATED fight_stats[epoch][last]} {
    #FORMAT {vitals[last_updated]} {#T};
    #IF {$vitals[经验] >= $downexp[threshold_exp]} {
        #ECHO {当前经验 $vitals[经验], 大于 $downexp[threshold_exp]。降经验开始};
        去降经验;
    };
}

#EVENT {VARIABLE UPDATED env[last_login]} {
    #EVENT {VARIABLE UPDATED env[room_updated]} {
        #IF {"$env[room]" == "驿站"} {
            #UNEVENT {VARIABLE UPDATED env[room_updated]};
            #ECHO {[Debug]: 开始自杀。};
            enable dodge duan-steps;
            enable move duan-steps;
            hp;
            w;s;大理-跳崖;
        };
    };
};

#ACTION {^你必须连线十秒后才能离开游戏。$} {
    
    #delay 2 {quit};
} {1};

#ACTION {^你正忙着呢，暂时无法离开游戏。$} {
    #delay 2 {quit};
} {5};


#TICKER {stuck_check} {

    #IF {"$downexp[room]" == "" || "$downexp[room]" == "$env[room]"} {
        #RETURN {};
    };

    #LOCAL timestamp %T;
    #LOCAL time_in_one_room {};
    #MATH time_in_one_room {($timestamp - $env[room_updated])};
    #ECHO {在当前房间时间: $time_in_one_room 秒。};
    #IF {$time_in_one_room > 3600} {
        quit;
    };
} {};


#ALIAS {大理-跳崖} {
    #ECHO {大理-跳崖-async};
    #5 w;nw;nu;nd;nw;wu;#2 n;nu;#2 w;nw;nu;nw;wu;
    #EVENT {VARIABLE UPDATED env[room_updated]} {
        #SWITCH {"$env[room]"} {
            #CASE {"山路"} {
                #SWITCH {"$env[exits]"} {
                    #CASE {" southdown 和 northup"} {cmd {nu}};
                    #CASE {" northeast 和 southdown"} {cmd {climb mountain}};
                    #CASE {" north 和 southdown"} {cmd {climb mountain}};
                };                
            };
            #CASE {"山顶"} {
                #UNEVENT {VARIABLE UPDATED env[room_updated]};
                n;e;n;n;e;e;s;s;
                #EVENT {VARIABLE UPDATED env[room_updated]} {
                    #SWITCH {"$env[room]"} {
                        #CASE {"鬼门关"} {
                            你死了;
                        };
                        #CASE {"城隍庙"} {
                            #UNEVENT {VARIABLE UPDATED env[room_updated]};
                            返回战斗地点;
                        };
                    };                    
                };
                #delay {0.2} {
                    jump down;
                };
            };                        
        };
    };
    #delay {0.2} {
        nu;wu;
    };
} {1};