#NOP Command Engine

#CLASS datetime READ {./pkg/pkg_datetime.tin};

#LIST {Command[history][q]} CREATE {};
#VARIABLE {Command[history][cnt]} {0};

#ALIAS Cmd {
    #LOCAL commands {%1};
    #LOCAL cnt {%2};
    #LOCAL epoch @GetEpoch{};
    #FOREACH $commands _c {
        #IF {"$_c" == ""} {
            #CONTINUE;
        };
        #ECHO {@Timestamp{} 输入指令: $_c};
        $_c;
    };
    #LIST {Command[history][q]} ADD {
        {
            {epoch} {$epoch}
            {cnt} {$cnt}
        }
    };
    #MATH Command[history][cnt] {$Command[history][cnt] + $cnt};
};

#ALIAS PurgeHistory {
    #LOCAL n {};
    #LOCAL head {0};
    #LOCAL next {1};
    #LIST Command[history][q] SIZE n;
    #LOCAL epoch @GetEpoch{};
    #WHILE {$head < $n} {
        #LOCAL c {$Command[history][q][$next]};
        #IF {$epoch - $c[epoch] < 10} {
            #BREAK;
        };
        #MATH Command[history][cnt] {$Command[history][cnt] - $c[cnt]};
        #MATH head {$head + 1};
        #MATH next {$next + 1};
    };
    #IF {$head > 0} {
        #LIST Command[history][q] DELETE 1 $head;
    };
};

#FUNCTION GetCommandCnt {
    PurgeHistory;
    #LOCAL total_cnt {0};
    #FOREACH *Command[history][q][%*] {x} {
        #MATH {total_cnt} {$total_cnt + $Command[history][q][$x][cnt]};
    };
    #RETURN $total_cnt;
};
