#NOP Learn Module

#CLASS vitals READ {./pkg/pkg_vitals.tin};
#CLASS datetime READ {./pkg/pkg_datetime.tin};
#CLASS utils READ {./pkg/pkg_utils.tin};
#CLASS command READ {./pkg/pkg_command.tin};
#CLASS print READ {./pkg/pkg_print.tin};
#CLASS learn_function READ {./pkg/pkg_learn_function.tin};

#EVENT {VARIABLE UPDATED vitals[last_updated]} {
    restore;
    learn_from_master_with_research $vitals[精神_max];
}

#TICKER {ticker_checkbusy} {
    Cmd {hp} {1};
} {1};


#ALIAS restore {
    #LOCAL xiqi {@RestoreFull{$vitals[精神];$vitals[精神_max];$xunhuan[min_restore]}};
    #IF {$xiqi > 0} {
        Cmd {xiqi $xiqi} {1};
    };
}

#ALIAS learn_from_master_no_research {
    #LOCAL js {%1};
    #IF {"$js" == ""} {
        #LOCAL js $vitals[精神_max];
    };

    #LOCAL qn {1};
    #LOCAL js_p_qn {$Learn[js_per_qn]};
    #LOCAL sk_target_lv @GetSkillToLearn{};
    #LOCAL sk {*sk_target_lv[+1]};
    #LOCAL target_lv {$sk_target_lv[+1]};

    Info {sk: $sk, target lv: $target_lv};

    #LOCAL sk_curr $skills[$sk];
    #LOCAL sk_curr_lv $sk_curr[lv];
    #LOCAL sk_curr_prof $sk_curr[prof];

    #LOCAL qn_before_level @GetMinQnBeforeLevel{$sk_curr_lv;$sk_curr_prof;$score[悟性]};
    #MATH qn {($js - 1) / $js_p_qn};
    #IF {$qn > $qn_before_level && $sk_curr_lv + 1 == $target_lv} {
        #MATH qn {$qn_before_level + 1};
    };
    Info {qn_before_level: $qn_before_level, qn to use: $qn, curr level: $sk_curr_lv, target lv: $target_lv};
    Cmd {learn $sk from $Learn[master] with $qn;skills} {2};
}

#ALIAS learn_from_master_with_research {
    #LOCAL js {%1};
    #IF {"$js" == ""} {
        #LOCAL js $vitals[精神_max];
    };

    #LOCAL qn {1};
    #LOCAL js_p_qn {$Learn[js_per_qn]};
    #LOCAL sk_target_lv @GetSkillToLearn{};
    #LOCAL sk {*sk_target_lv[+1]};
    #LOCAL target_lv {$sk_target_lv[+1]};

    Info {sk: $sk, target lv: $target_lv};

    #LOCAL sk_curr $skills[$sk];
    #LOCAL sk_curr_lv $sk_curr[lv];
    #LOCAL sk_curr_prof $sk_curr[prof];

    #LOCAL qn_before_level @GetMinQnBeforeLevel{$sk_curr_lv;$sk_curr_prof;$score[悟性]};
    Info {qn_before_level: $qn_before_level, curr level: $sk_curr_lv, target lv: $target_lv};
    #IF {"$qn_before_level" == "0"} {
        Cmd {research $sk $xunhuan[qn_per_res]} {1};
        #RETURN {};
    };

    #MATH qn {($js - 1) / $js_p_qn};
    
    #IF {$qn > $qn_before_level && ($sk_curr_lv >= 100 || $sk_curr_lv + 1 == $target_lv)} {
        #LOCAL qn {$qn_before_level};
    };
    Info {qn_before_level: $qn_before_level, qn to use: $qn, curr level: $sk_curr_lv, target lv: $target_lv};
    Cmd {learn $sk from $Learn[master] with $qn} {1};
    Cmd {skills} {1};
}

Info {开启学习模块};
Cmd {skills;score;research literate 1} {3};
#VARIABLE {Busy[next_check]} {0};
