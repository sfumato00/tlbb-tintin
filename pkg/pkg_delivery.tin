#NOP Delivery Module

#CLASS consts READ {./const.tin};
#CLASS graph read {./pkg/pkg_map_graph.tin};
#CLASS logging READ {./pkg/pkg_logging.tin};
#CLASS command READ {./pkg/pkg_command.tin};
#CLASS map_functions READ {./pkg/pkg_map_functions.tin};
#CLASS quit READ {./pkg/pkg_quit.tin};
#CLASS checkbusy READ {./pkg/pkg_checkbusy.tin};

#ALIAS {go_deliver} {
    #EVENT {VARIABLE UPDATED env[travel_callback_hook]} {
        #IF {@_assertRoomExitsKey{$Delivery[room_key]}} {
            #UNEVENT {VARIABLE UPDATED env[travel_callback_hook]};
            Cmd {give $Delivery[next_guy] $Delivery[npc]} {1};
        };
    };
    goto {$Delivery[room_key]};
} {5};


#ACTION {^%*给你一位%u。$} {

    #LOCAL {cn_name} {%2};
    #VARIABLE {Delivery[npc]} {$NPC_NAME_CN_EN[$cn_name]};
    Log {%0} {Info} {./log/delivery_$Profile[username].log};
    #IF {"$Delivery[npc]" == ""} {
        #LOCAL msg {无法识别的NPC: $cn_name};
        Log {$msg} {DEBUG} {./log/delivery_$Profile[username].log};
        #ECHO {msg};
        #IF {"$env[last_buffer_dump]" == "" || @GetEpoch{} - $env[last_buffer_dump]  > 60} {
            buffer_dump {delivery_unknown_npc};
            #VARIABLE {env[last_buffer_dump]} {@GetEpoch{}};
        };
        Cmd {drop all} {1};
        #RETURN {};
    };

    #SHOW {npc: $Delivery[npc], weapon: $NPC_WEAPON[$cn_name]};
    #IF {"$NPC_WEAPON[$cn_name]" != ""} {
        Cmd {get $NPC_WEAPON[$cn_name] from $Delivery[npc]} {1};
    };
    #ELSE {
        Cmd {get all from $Delivery[npc]} {1};
        #IF {"$env[last_buffer_dump]" == "" || @GetEpoch{} - $env[last_buffer_dump]  > 120} {
            buffer_dump {delivery_unknown_weapon};
            #VARIABLE {env[last_buffer_dump]} {@GetEpoch{}};
        };
    };
    delivery_timeout;
    送快递;
} {5};

#ACTION {^您要将另一个连线中的相同人物赶出去，取而代之吗？} {
    y;
} {5};

#ACTION {%*慢慢睁开眼睛，清醒了过来。} {
    Log {%0} {Info} {./log/delivery_$Profile[username].log};
    Cmd {quit} {1};
};

#ACTION {^$Delivery[next_guy_cn]走了过来。$} {
    Cmd {
        give $Delivery[next_guy] $Delivery[npc];
    } {2};
};

#ACTION {^你身上没有这样东西。$} {
    Cmd {quit} {1};
};

#ACTION {^你给%*一位%*。} {
    Log {%0} {Info} {./log/delivery_$Profile[username].log};
    Cmd {quit} {1};
};

#ACTION {^这个方向没有出路。} {
    #IF {"$env[last_buffer_dump]" == "" || @GetEpoch{} - $env[last_buffer_dump]  > 300} {
        buffer_dump {delivery};
        #VARIABLE {env[last_buffer_dump]} {@GetEpoch{}};
    };
};

#ACTION {^这里没有这个人。$} {
    Cmd {quit} {1};
};

#ALIAS delivery_timeout {
    #DELAY 25 {
        #TICKER {ticker_timeout} {quit} {1};
    };
};


#EVENT {VARIABLE UPDATED Busy[busy]} {
    #IF {"$Busy[busy]" == "1"} {
        #DELAY 1 {
            Cmd {
                give $Delivery[next_guy] $Delivery[npc];
                quit;
            } {2};
        };
    };
}

#EVENT {VARIABLE UPDATED env[room_updated]} {
    #IF {"$env[room]" == "城隍庙"} {
        buffer_dump {delivery};
        #VARIABLE {env[last_buffer_dump]} {@GetEpoch{}};
        Cmd {quit} {1};
    };

    #IF {"$env[room]" == "鬼门关"} {
        Cmd {#2 n;w;ask $Profile[username] about 回家} {4};
    };
};

#EVENT {VARIABLE UPDATED env[last_reconnect]} {
    Cmd {look} {1};
};

#TICKER {ticker_stay} {l} {300};