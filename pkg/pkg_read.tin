#CLASS read OPEN;

#VARIABLE THROTTLE {8};

#ALIAS {feedback_loop_read} {
    #LOCAL time {};
    #FORMAT time {%T};
    #NOP #ECHO {[Debug] assert_not_busy: busy until $busy[until], current timestamp: $time};
    #IF {$time < $busy[until]} {
        #RETURN {};
    };

    #IF {$vitals[内力] < $vitals[内力_max]} {
                
        #LOCAL yunqi {@RestoreFull{$vitals[气血];$vitals[气血_max];$xunhuan[min_restore]}};
        #LOCAL xiqi {@Restore{$vitals[精神];$vitals[精神_max];$xunhuan[min_restore]}};
        #IF {$xiqi > 0} {
            cmd {xiqi $xiqi};
        };
        
        #IF {$yunqi > 0} {
            cmd {yunqi $yunqi};
        };
        busy_dazuo $vitals[气血_max];
        #RETURN {};
    } {
        #LOCAL full_xiqi {@RestoreFull{$vitals[精神];$vitals[精神_max];$xunhuan[min_restore]}};
        
        #IF {$full_xiqi > 0} {
            cmd {xiqi $full_xiqi};
        };
        
        #IF {"$read[book]" == "yijinjing"} {
            #LOCAL full_xiqi_max {@RestoreFull{0;$vitals[精神_max];$xunhuan[min_restore]}};
            #LOCAL multiple {};
            #MATH multiple {($vitals[内力] - $vitals[内力_max]) / $full_xiqi_max + 1};
            #IF {$multiple > $THROTTLE} {
                #LOCAL {multiple} {$THROTTLE};
            };
            #$multiple {
                cmd {du $read[book] $vitals[精神_max];xiqi $full_xiqi_max;};
            };
        };
        #ELSE {
            busy_du $read[book] $vitals[精神_max];
        };              
    };
} {5};

#EVENT {VARIABLE UPDATED vitals[经验]} {
    #NOP #ECHO {#EVENT {VARIABLE UPDATED vitals[内力]}: feedback_loop;};
    feedback_loop_read;
};

#EVENT {VARIABLE UPDATED busy[updated]} {
    cmd {skills};
    cmd {hp};
};

#ALIAS {stop} {
    #ECHO {关闭 读书 模块};
    cmd {put $read[book] in bag};
    feedback_loop_idle;
    #CLASS read kill;
} {5};


#ACTION {你的%u功力不足，无法继续练%u。} {
    cmd {put $read[book] in bag};
}

#ACTION {你发现上面所说的对你而言都太浅了，没有学到任何东西。} {
    cmd {put $read[book] in bag};
}

#ACTION {^你要读什麽？$} {
    get book from bag 2;
} {5};

#ACTIOn {^阿弥陀佛，整天就知道打打杀杀，你还是先去念念金刚经吧！！$} {
    cmd {put book in bag};
};

#ACTION {^你专心地研读了一会儿易筋经，可是只觉得头大如斗。看来以你现在的经验值无法理解易筋经。$} {
    cmd {put yijinjing in bag;put bag in bag 2;put bag in bag 2};
} {5};

#FUNCTION CalReadParam {
    #LOCAL {prof_per_read} {%1};
    #LOCAL {max_js} {%2};
    #LOCAL {curr_level} {%3};
    #LOCAL {curr_prof} {%4};
    #LOCAL {js_per_read} {5};
    #IF {"$jx_per_read" != ""} {
        #LOCAL {js_per_read} {%5};
    };
    
    #LOCAL next_level {0};
    #MATH next_level {$curr_level + 1};
    #MATH max_prof {$next_level * $next_level + 1};

    #LOCAL diff {0};
    #MATH diff {$max_prof - $curr_prof};
    #ECHO {[Debug]: diff: $diff};
    #LOCAL times {};
    #MATH times {$diff / $prof_per_read + 1};
    #ECHO {[Debug]: times: $times};
    #LOCAL needjs {};
    #MATH {need_js} {$times * $js_per_read};
    #ECHO {[Debug]: need_js: $need_js, times: $times};
    #RETURN {@Min{$max_js;$need_js}};
}

#CLASS read CLOSE;
