#NOP death log module

#CLASS datetime READ {./pkg/pkg_datetime.tin};
#CLASS logging READ {./pkg/pkg_logging.tin};
#CLASS vitals READ {./pkg/pkg_vitals.tin};
#CLASS quit READ {./pkg/pkg_quit.tin};

#ACTION {^你死了。$} {
    buffer_dump {death};
} {1};


#ACTION {^看起来%u想杀死你！$} {
    Log {%0} {Warn} {./log/kill_$username_@Timestamp{}.log};
    #EVENT {SESSION DISCONNECTED} {
        buffer_dump {kill};
        #UNTICKER {ticker_quit};
    };
    emergency_quit;
};


#EVENT {VARIABLE UPDATED vitals[气血_max]} {
    #IF {$vitals[气血%] < 95 || $hp_percentage < 50} {
        #ECHO {<faa>=====> 非正常状态, 紧急处理 <=======};
        #SHOW {<faa>=====> 非正常状态, 紧急处理 <=======} 2 10;
        Cmd {surrender} {1};
        #IF {"$env[last_buffer_dump]" == "" || @GetEpoch{} - $env[last_buffer_dump]  > 60} {
            buffer_dump {fight};
            #VARIABLE {env[last_buffer_dump]} {@GetEpoch{}};
        };
    };
}

#ALIAS {emergency_quit} {
    #EVENT {SESSION DISCONNECTED} {
        buffer_dump {emergency_quit};
        #END;
    };
    #IF {"$env[last_login]" == "" && "$env[last_reconnect]" == ""} {
        #VARIABLE attemp_quit_after {0};
    };
    #ELSEIF {"$env[last_reconnect]" == ""} {
        #MATH attemp_quit_after {$env[last_login] + 9};
    };
    #ELSEIF {"$env[last_login]" == ""} {
        #MATH attemp_quit_after {$env[last_reconnect] + 9};
    };
    #ELSE {
        #VARIABLE attemp_quit_after {@Max{$env[last_reconnect];$env[last_login]} + 9};
    };
    #TICKER {ticker_quit} {
        #IF {@GetEpoch{} >= $attemp_quit_after} {
            Cmd {quit} {1};
        };
    } {0.2};
    #IF {@GetEpoch{} >= $attemp_quit_after} {
        Cmd {quit} {1};
    };
} {5};