#NOP ------------------------ Alias ----------------------------------

#CLASS tuna OPEN;

#CLASS utils READ {./pkg/pkg_utils.tin};
#CLASS vitals READ {./pkg/pkg_vitals.tin};
#CLASS stats READ {./pkg/pkg_stats.tin};
#CLASS print READ {./pkg/pkg_print.tin};
#CLASS command READ {./pkg/pkg_command.tin};
#CLASS checkbusy READ {./pkg/pkg_checkbusy.tin};
#CLASS datetime READ {./pkg/pkg_datetime.tin};

#VARIABLE {Busy[next_check]} {0};

#EVENT {VARIABLE UPDATED vitals[last_updated]} {
    feedback_loop_tuna;
}

#EVENT {VARIABLE UPDATED Busy[busy]} {
    #IF {"$Busy[busy]" == "0"} {
        Cmd {hp} {1};
    };
};

#TICKER {ticker_checkbusy} {
    #IF {@GetEpoch{} >= $Busy[next_check]} {
        Cmd {dazuo 1} {1};
    };
} {1};

#ALIAS {stop} {
    Info {关闭 吐纳 模块};
    feedback_loop_idle;
    #CLASS tuna kill;
} {5};


#ALIAS {feedback_loop_tuna} {
    #LOCAL time {@GetEpoch{}};

    #LOCAL xiqi {@RestoreFull{$vitals[精神];$vitals[精神_max];$xunhuan[min_restore]}};
    #LOCAL yunqi {@RestoreFull{$vitals[气血];$vitals[气血_max];$xunhuan[min_restore]}};
    #LOCAL max_full_xiqi {@RestoreFull{0;$vitals[精神_max];$xunhuan[min_restore]}};

    #IF {$xiqi > 0} {
        Cmd {xiqi $xiqi} {1};
    };
    #IF {$yunqi > 0} {
        Cmd {yunqi $yunqi} {1};
    };

    #MATH {vitals[内力]} {$vitals[内力] - $yunqi - $xiqi};
    #IF {$vitals[内力] < $vitals[内力_max] / 2} {
        Cmd {dazuo $vitals[气血_max]} {1};
        #MATH Busy[next_check] {$time + 7};
        #RETURN {};
    };
    
    #LOCAL available_neili {$vitals[内力]};

    #ECHO {[Debug] available_neili: $available_neili, max_full_xiqi: $max_full_xiqi, GetCommandCnt: @GetCommandCnt{}};

    #WHILE {$available_neili > $max_full_xiqi && @GetCommandCnt{} < 40} {
        Cmd {
            tuna $vitals[精神_max];
            xiqi $max_full_xiqi;
        } {2};
        #MATH available_neili {$available_neili - $max_full_xiqi};
    };
    #MATH Busy[next_check] {$time + 1};
} {5};

#CLASS tuna CLOSE;

Cmd {score;enable;skills;hp} {4};