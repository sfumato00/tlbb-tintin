#NOP ------------------------ Alias ----------------------------------

#CLASS tuna OPEN;

#VARIABLE THROTTLE {8};

#ALIAS {feedback_loop_tuna} {
    #LOCAL time {};
    #FORMAT time {%T};
    #NOP #ECHO {[Debug] assert_not_busy: busy until $busy[until], current timestamp: $time};
    #IF {$time < $busy[until]} {
        #RETURN {};
    };
    
    #IF {$vitals[内力] < $vitals[内力_max]} {
                
        #LOCAL yunqi {@RestoreFull{$vitals[气血];$vitals[气血_max];$xunhuan[min_restore]}};
        #LOCAL xiqi {@Restore{$vitals[精神];$vitals[精神_max];$xunhuan[min_restore]}};
        #IF {$xiqi > 0} {
            cmd {xiqi $xiqi};
        };
        
        #IF {$yunqi > 0} {
            cmd {yunqi $yunqi};
        };
        busy_dazuo $vitals[气血_max];
        #RETURN {};
    } {
        #LOCAL full_yunqi {@RestoreFull{$vitals[气血];$vitals[气血_max];$xunhuan[min_restore]}};
        #LOCAL max_full_xiqi {@RestoreFull{0;$vitals[精神_max];$xunhuan[min_restore]}};
        #LOCAL full_xiqi {@RestoreFull{$vitals[精神];$vitals[精神_max];$xunhuan[min_restore]}};
        
        #LOCAL n_cmd {0};

        #IF {$full_xiqi > 0} {
            cmd {xiqi $full_xiqi};
            #MATH {n_cmd} {$n_cmd + 1};
        };
        
        #IF {$full_yunqi > 0} {
            cmd {yunqi $full_yunqi};
            #MATH {n_cmd} {$n_cmd + 1};
        };

        #LOCAL available_neili {$vitals[内力] - $full_yunqi - $full_xiqi};

        #WHILE {$available_neili > $max_full_xiqi && $n_cmd < $THROTTLE} {
            cmd {tuna $vitals[精神_max]};
            cmd {xiqi $max_full_xiqi};
            #MATH {n_cmd} {$n_cmd + 2};
        };
    };
} {5};

#EVENT {VARIABLE UPDATED vitals[经验]} {
    #NOP #ECHO {#EVENT {VARIABLE UPDATED vitals[内力]}: feedback_loop;};

    #IF {$skills[literate][lv] > 0} {
        #MATH {tuna[潜力_max]} {$skills[literate][lv] * 10};
    };

    #IF {$enables[literate] > 0} {
        #MATH {tuna[潜力_max]} {$tuna[潜力_max] + $enables[literate] * 2};
    };

    feedback_loop_tuna;
};

#EVENT {VARIABLE UPDATED busy[updated]} {
    cmd {hp};
};

#ALIAS {stop} {
    #ECHO {关闭 吐纳 模块};
    feedback_loop_idle;
    #CLASS tuna kill;
} {5};

#CLASS tuna CLOSE;