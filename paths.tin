#NOP Paths

#ALIAS {开封-大理} {#9 s;sw;#9 s};
#ALIAS {开封-东安门} {#6 e};
#ALIAS {开封-洛阳} {#16 w;}

#ALIAS {大理-开封} {#9 n;ne;#9 n}
#alias {大理-洛阳} {#9 n;ne;#6 w;#3 n;#7 w;}

#ALIAS {洛阳-天水} {#8 w;}
#ALIAS {洛阳-无锡} {#7 s;e;s;#4 w;#4 n;nw;nu;wd;ne;n;ne;n;w;#3 n} {5};
#alias {洛阳-西夏} {
    洛阳-天水;
    天水-黑沼泽;
    #DELAY {6} {
        黑沼泽-西夏;
    };
};
#ALIAS {洛阳-开封} {#16 e;}
#ALIAS {洛阳-东安门} {洛阳-开封;开封-东安门} {5};

#ALIAS {洛阳-登封} {#7 e;#3 s} {5};
#ALIAS {洛阳-大理} {洛阳-登封;登封-大理} {5};


#ALIAS {登封-大理} {#6 e;sw;#5 s;#3 s;s;} {5};

#ALIAS {初生点-食物屋} {#5 s}
#ALIAS {食物屋-龙门客栈} {#6 s;d}
#ALIAS {龙门客栈-开封} {w;#5 n}
#ALIAS {龙门客栈-大理} {w;#5 s;sw;#9 s}


#ALIAS {无锡-洛阳} {#3 s;e;s;sw;s;sw;se;sd;se;#4 s;#4 e;n;w;#7 n} {5};
#ALIAS {无锡-npc副本} {#2 n;enter door;} {5};
#ALIAS {npc副本-无锡} {#3 s;} {5};


#ALIAS {龙门客栈-洛阳} {龙门客栈-开封;开封-洛阳;}

#ALIAS {天水-洛阳} {#8 e;}
#ALIAS {洛阳-松鹤楼} {n;w;}
#ALIAS {松鹤楼-洛阳} {e;s;}


#ALIAS {npc-go} {
    松鹤楼-洛阳;
    洛阳-无锡;
    无锡-npc副本;
}

#ALIAS {npc-back} {
    get sixhammer from guard;
    get %1;
    npc副本-无锡;
    无锡-洛阳;
    洛阳-松鹤楼;
} {5};


#ALIAS {大雄宝殿-老和尚} {
    n;n;e;n;n;n;n;w;n;n;n;n;nw;nu;wd;wu;nu;nu;
} {5};

#ALIAS {老和尚-大雄宝殿} {
    sd;sd;ed;eu;sd;se;#4 s;e;#4 s;w;s;s;
} {5};


#ALIAS {城隍庙-开封} {
    n;#5 w;
} {5};

#ALIAS {鬼门关-黑店} {
    #2 n;e;
} {5};


#ALIAS {天贶殿-神憩小舍} {
    #3 n;#2 nw;#2 nu;n;nu;n;nu;n;nu;ne;nu;
} {5};


#ALIAS {神憩小舍-天贶殿} {
    sd;se;sd;s;sd;s;sd;s;#2 sd;#2 se;#3 s;
} {5};

#ALIAS {天水-西夏} {
    天水-黑沼泽;
    #DELAY {3} {
        黑沼泽-西夏;
    };    
} {5};

#ALIAS {天水-黑沼泽} {
    #2 w;wu;wd;#3 w;s;n;w;
} {5};

#ALIAS {黑沼泽-西夏} {
    #ALIAS {local_helper} {
        #SWITCH {"$env[room]"} {
            #CASE {"黑沼泽"} {
                
                #EVENT {VARIABLE UPDATED env[room_updated]} {
                    local_helper;
                };
                #SWITCH {"$env[exits]"} {
                    #CASE {" north 和 southwest"} {
                        cmd {n};
                    };
    
                    #CASE {" south、north 和 west"} {
                        cmd {n};
                    };
    
                    #CASE {" south、north、east 和 west"} {
                        #IF {$env[odd] == 0} {cmd {w};} {cmd {s};};
                        #MATH {env[odd]} {1 - $env[odd]};
                    };
    
                    #DEFAULT {
                        #ECHO {debug: **** 错误的房间。停止。***};
                        #UNVARIABLE {env[odd]};
                        #UNEVENT {VARIABLE UPDATED env[room_updated]};
                    };
                };
            };
            #CASE {"大道"} {
                #UNVARIABLE {env[odd]};
                #UNEVENT {VARIABLE UPDATED env[room_updated]};
                #4 {cmd {n}};
            };
            #DEFAULT {
                #UNEVENT {VARIABLE UPDATED env[room_updated]};
            }
        };
    } {5};

    #VARIABLE {env[odd]} {0};
    local_helper;
} {5};

#ALIAS {西夏-天水大道} {
    #2 s;兴庆城城门-天水大道
} {5};

#ALIAS {兴庆城城门-天水大道} {
    #EVENT {VARIABLE UPDATED env[room_updated]} {
        #IF {"$env[room]" == "黑沼泽"} {
            #UNEVENT {VARIABLE UPDATED env[room_updated]};
            黑沼泽-天水大道;
        };
    };
    兴庆城城门-黑沼泽;
} {5};

#ALIAS {兴庆城城门-黑沼泽} {#2 s;e;} {5};

#ALIAS {黑沼泽-天水大道} {
    #ALIAS {local_helper} {
        #IF {"$env[room]" == "黑沼泽"} {
            #EVENT {VARIABLE UPDATED env[room_updated]} {
                local_helper;
            };
            #SWITCH {"$env[exits]"} {
                #CASE {" north 和 southwest"} {
                    #UNVARIABLE {env[odd]};
                    #UNEVENT {VARIABLE UPDATED env[room_updated]};
                    sw;n;e;e;eu;ed;
                    RunCallback;
                };

                #CASE {" south、north 和 west"} {
                    cmd {s};
                };

                #CASE {" south、north、east 和 west"} {
                    #IF {$env[odd] == 0} {
                        cmd {e};
                    } {
                        cmd {s};
                    };
                    #MATH {env[odd]} {1 - $env[odd]};
                };

                #DEFAULT {
                    #ECHO {debug: **** 错误的房间。停止。***};
                    #UNVARIABLE {env[odd]};
                    #UNEVENT {VARIABLE UPDATED env[room_updated]};                        
                };
            };
        } {
            #ECHO {debug: **** 错误的房间。停止。***};
            #UNVARIABLE {env[odd]};
            #UNEVENT {VARIABLE UPDATED env[room_updated]};
        }
    } {5};

    #VARIABLE {env[odd]} {0};
    local_helper;
} {5};


#ALIAS {城隍庙-神憩小舍} {
    n;
    #EVENT {VARIABLE UPDATED env[room_updated]} {
        #IF {"$env[room]" == "东安门"} {
            #UNEVENT {VARIABLE UPDATED env[room_updated]};
            黄土大道-神憩小舍;
        };
    };
    e;
} {5};


#alias {黄土大道-神憩小舍} {
    #EVENT {VARIABLE UPDATED env[room_updated]} {
        #IF {"$env[exits]" == " east 和 west"} {
            #DELAY 0.1 {
                cmd {e};
            };
        } {
            #UNEVENT {VARIABLE UPDATED env[room_updated]};
            #DELAY 12 {
                se;e;#13 n;
                #3 n;#2 nw;nu;#3 {nu;n};nu;ne;nu;
                init_task;              
            };            
        };
    };
    cmd {e};
};


#ALIAS {大理-跳崖} {
    #ECHO {大理-跳崖-async};
    #5 w;nw;nu;nd;nw;wu;#2 n;nu;#2 w;nw;nu;nd;nw;wu;n;n;nu;wu;
    #EVENT {VARIABLE UPDATED env[room_updated]} {

        #SWITCH {"$env[room]"} {
            #CASE {"山路"} {
                #IF {"$env[exits]" == " southdown 和 northup"} {
                    nu;
                } {
                    climb mountain;
                };
            };
            #CASE {"山顶"} {
                #UNEVENT {VARIABLE UPDATED env[room_updated]};
                n;e;n;n;e;e;s;s;jump down;
            };
        };
    };
    #delay {0.2} {
        climb mountain;
    };
} {1};

#alias RunCallback {
    #IF {"$callback" != ""} {
        cmd {$callback};
    };    
}