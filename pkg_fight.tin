#GAG {似乎有些疲惫，但是仍然十分有活力。 )}
#GAG {动作似乎开始有点不太灵光，但是仍然有条不紊。 )}
#GAG {气喘嘘嘘，看起来状况并不太好。 )}
#GAG {看起来可能有些累了。 )}
#GAG {看起来充满活力，一点也不累。 )}

#TICKER {ticker_feedback_loop} {hp} {15};

#VARIABLE {fight_stats} {
    {epoch} {
        {start} {0}
    }
};

#VARIABLE {MIN_IN_SECOND} {60};
#VARIABLE {MIN_IN_MICRO_SECOND} {60M};

#ALIAS {feedback_loop} {
    cmd {say @CalNpcMsg{}};
    #IF {$vitals[饮水] < $vitals[饮水_max]} {
        cmd {drink tong};
    };

    #IF {$vitals[食物] < $vitals[食物_max]} {
        cmd {eat pig};
    };

    #LOCAL {hp_percentage} {0};
    #MATH {hp_percentage} {$vitals[气血] * 100 / $vitals[气血_max]};

    #IF {$hp_percentage >= 50} {
        fight_npcs;
    } {
        #IF {$hp_percentage < 20} {
            cmd {surrender};
        };
    };

    #FORMAT time {%U};

    #LOCAL {t_total} {0};
    #LOCAL {t_delta} {0};

    #LOCAL {exp} {$vitals[经验]};
    #LOCAL {qn} {$vitals[潜能]};

    #LOCAL {exp_delta} {0};
    #LOCAL {qn_delta} {0};

    #LOCAL {exp_velocity} {0};
    #LOCAL {qn_velocity} {0};

    #LOCAL {exp_mean} {0};
    #LOCAL {qn_mean} {0};

    #IF {$fight_stats[epoch][start] == 0} {
        init_fight;
        #VARIABLE {fight_stats[character]} {$username};

        #VARIABLE {fight_stats[epoch]} {
            {start} {$time}
            {last} {$time}
        };

        #VARIABLE {fight_stats[经验]} {
            {start} {$exp}
            {last} {$exp}
            {velocity} {
                {max} {0}
                {mean} {0}
            }
        };
        #VARIABLE {fight_stats[潜能]} {
            {start} {$qn}
            {last} {$qn}
            {velocity} {
                {max} {0}
                {mean} {0}
            }
        };
    };

    #MATH {t_delta} {$time - $fight_stats[epoch][last]};
    #MATH {t_total} {$time - $fight_stats[epoch][start]};

    #MATH {exp_delta} {$exp - $fight_stats[经验][last]};
    #MATH {qn_delta} {$qn - $fight_stats[潜能][last]};

    #MATH {exp_velocity} {$exp_delta * $MIN_IN_MICRO_SECOND / $t_delta};
    #MATH {qn_velocity} {$qn_delta * $MIN_IN_MICRO_SECOND / $t_delta};

    #MATH {fight_stats[经验][velocity][mean]} {($exp - $fight_stats[经验][start]) * $MIN_IN_MICRO_SECOND / $t_total};
    #MATH {fight_stats[潜能][velocity][mean]} {($qn - $fight_stats[潜能][start])  * $MIN_IN_MICRO_SECOND / $t_total};

    #NOP #SHOWME {$time, $t_delta, $exp_delta, $exp_velocity, $fight_stats[经验][velocity][max], @Min{$fight_stats[经验][velocity][max];$exp_velocity}};

    #VARIABLE {fight_stats[经验][velocity][max]} {@Max{$fight_stats[经验][velocity][max];$exp_velocity}};
    #VARIABLE {fight_stats[潜能][velocity][max]} {@Max{$fight_stats[潜能][velocity][max];$qn_velocity}};

    #VARIABLE {fight_stats[epoch][last]} {$time};
    #VARIABLE {fight_stats[经验][last]} {$exp};
    #VARIABLE {fight_stats[潜能][last]} {$qn};

    #NOP generate message;
    #LOCAL {duration_in_min} {0};
    #MATH {duration_in_min} {$fight_stats[duration] / $MIN_IN_MICRO_SECOND};

    #FORMAT {exp_msg} {经验增速: %d (最大 %d // 平均 %d)} {$exp_velocity} {$fight_stats[经验][velocity][max]} {$fight_stats[经验][velocity][mean]};
    #FORMAT {qn_msg} {潜能增速: %d  (最大 %d // 平均 %d)} {$qn_velocity} {$fight_stats[潜能][velocity][max]} {$fight_stats[潜能][velocity][mean]};

    #DELAY {0.1} {
        #SHOWME {$exp_msg};
        #SHOWME {$qn_msg};
    };
}

#ALIAS {fight_npcs} {
    #FOREACH {*npc_list[%*]} {x} {
        #FOREACH {*npc_list[$x][%*]} {i} {
            cmd {fight $x $npc_list[$x][$i]};
        };
    };
} {5};

#ALIAS {init_fight} {
    #ECHO {开始练功};
    remove cloth;
    drop cloth;
    wear cloth;
    set no_give 1;
    set no_msg 1;
    set mute2;
    enable dodge none;
    enable move none;
}

#ACTION {您要将另一个连线中的相同人物赶出去，取而代之吗？} {
    #DELAY {15} {
        cmd {no};
    };
}

#ACTION {^( 你摇头晃脑、歪歪斜斜地站都站不稳，眼看就要倒在地上。 )} {
    cmd {surrender};
}

#ACTION {慢慢睁开眼睛，清醒了过来。} {
    cmd {surrender};
    #DELAY {3.1} {
        init_fight;
    };
};

#EVENT {VARIABLE UPDATED vitals[经验]} {
    feedback_loop;
};


#FUNCTION CalNpcMsg {
    #LOCAL {msg} {};
    #FOREACH {*npc_list[%*]} {x} {
        #LOCAL m {$x: *npc_list[$x][+1]-*npc_list[$x][-1]};
        #LIST msg ADD $m;
    };
    #LIST msg COLLAPSE {, };
    #RETURN {$msg};
}